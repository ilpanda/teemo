if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


android {

    compileSdk Libs.dep.compile_sdk_version()
    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.ilpanda.teemo"
        }
        minSdk Libs.dep.min_sdk_version()
        targetSdk Libs.dep.target_sdk_version()
        versionCode Libs.dep.version_code()
        versionName Libs.dep.version_name()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            // 独立调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

allprojects {
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {


    implementation project(':arch-debug')

    // Kotlin
    implementation Libs.dep.kotlin_stdlib()
    implementation Libs.dep.kotlin_reflect()
    implementation Libs.dep.androidx_core()

    // butterknife
    implementation Libs.dep.butterknife()
    kapt Libs.dep.butterknife_compiler()

    // leakcanary
    debugImplementation Libs.dep.leakcanary_android()

    // google material
    implementation Libs.dep.material()

    testImplementation Libs.dep.test_junit()
    androidTestImplementation Libs.dep.test_androidx_junit()
    androidTestImplementation Libs.dep.test_androidx_espresso_core()


}