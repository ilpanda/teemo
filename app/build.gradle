if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


android {
    compileSdk rootProject.ext.android.compileSdkVersion
    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.ilpanda.teemo"
        }
        minSdk rootProject.ext.android.minSdkVersion
        targetSdk rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            // 独立调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

allprojects {
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {

    implementation project(':arch-debug')

    // Kotlin
    implementation rootProject.ext.dependencies['kotlin-std']
    implementation rootProject.ext.dependencies['kotlin-reflect']
    implementation rootProject.ext.dependencies['kotlin-core']

    // butterknife
    implementation rootProject.ext.dependencies.butterknife
    kapt rootProject.ext.dependencies.butterknifeCompiler

    // leakcanary
    debugImplementation rootProject.ext.debug['leakcanary']

    // google material
    implementation rootProject.ext.dependencies['material']

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'


}